## @file
#  @author Sylwester Arabas <slayoo@igf.fuw.edu.pl>
#  @copyright University of Warsaw
#  @date December 2011
#  @section LICENSE
#    GPL v3 (see the COPYING file or http://www.gnu.org/licenses/)

set xlabel "x"
set view 50,320
#set noxtics
set ztics .5
set ylabel "t"
#set noytics
#set cbrange [-.5:1]
#set cbtics .5
set zrange [.8:2.2]
set xyplane at 0
set palette defined ( .8 "red", 1 "red", 1 "blue", 2 "blue", 2 "green", 2.2 "green")
#set term svg
#set output "fig1.svg"
cmd = "../../icicle" \
  ." --dt 1 " \
  ." --grd.dx 1" \
  ." --grd.dy 1 " \
  ." --grd.dz 1" \
  ." --vel uniform --vel.uniform.u -.75" \
  ." --nt 5 --nx 90" \
  ." --slv serial" \
  ." --out gnuplot" \
  ." --ini boxcar --ini.boxcar.ax 20 --ini.boxcar.bx 40 --ini.boxcar.A0 1 " 
print cmd
set multiplot layout 1,2
  set title "MPDATA (iord=2,fct=0)"
  splot "<".cmd." --adv.mpdata.iord 2 --adv mpdata" using 0:-2:1 notitle with lines palette
  set title "MPDATA (iord=2,fct=1)"
  splot "<".cmd." --adv.mpdata.iord 2 --adv.mpdata.fct 1 --adv mpdata" using 0:-2:1 notitle with lines palette
unset multiplot
pause -1
